# $Id: PKGBUILD 198332 2016-12-07 18:09:47Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Doehni <dohny@gmx.de>

pkgname=gnome-do
pkgver=0.95.3
pkgrel=3
pkgdesc="Quickly search for many items present on your desktop or the web, and perform useful actions on those items"
arch=('i686' 'x86_64')
url="http://do.cooperteam.net/"
license=('GPL3')
depends=('gconf-sharp' 'gnome-desktop2' 'gnome-keyring-sharp' 'mono-addins' 'notify-sharp' 'gkeyfile-sharp')
makedepends=('intltool' 'gio-sharp')
source=(https://launchpad.net/do/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz{,.asc}
        03_dbus_sharp_2.diff
        fix-build.patch)
validpgpkeys=('972401D296309DE91E713C6F18B3F50FE5923C18') # Christopher James Halse Rogers
md5sums=('698ff640c8b54565725e9f836384c894'
         'SKIP'
         '4629501823d94099fbb9d613913839a6'
         '4ac79a8270bc6c1539f56b8f56043894')

prepare() {
  cd $pkgname-$pkgver

  # Fix build
  patch -Np1 -i ../fix-build.patch

  # Use dbus-sharp-2
  patch -Np1 -i ../03_dbus_sharp_2.diff

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
